# vim: filetype=zsh

autoload -U colors && colors

# usage: colour COLOUR WORD [WORD]
function colour() {
  local color=$1
  shift
  echo "%{$fg[${color}]%}${*}%{$reset_color%}"
}

function _username() {
  if (( UID == 0 )); then
    colour red 'root'
    return $?
  fi

  echo '%n'
}

function _last_status() {
  local return_code=$1
  if (( return_code != 0 )); then
    colour red "$1 "
  fi
}

# Show hostname if in an ssh session
function _ssh_hostname() {
  [[ -n "${SSH_CONNECTION}" ]] && echo '@%m'
}

function _show_mode() {
  case "${KEYMAP}" in
    main)  echo '>' ;;
    vicmd) echo '<' ;;
    *)     echo '>' ;;
  esac
}

function _git_current_branch() {
  if git rev-parse --is-inside-work-tree &>/dev/null; then
    git rev-parse --abbrev-ref HEAD
  fi
}

# # https://dougblack.io/words/zsh-vi-mode.html
function zle-line-init zle-keymap-select {
  VIM_PROMPT="%{$fg_bold[yellow]%} [% NORMAL]% %{$reset_color%}"
  _mode="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/} $EPS1"
  zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

function prompt_custom_setup() {
  prompt_opts=(cr subst percent)
  PS1='$(_username)$(_ssh_hostname) $(colour green %~)$(_show_mode) '
  RPS1='$(_last_status $?)$(_git_current_branch)${_mode}'
}

prompt_custom_setup "$@"
