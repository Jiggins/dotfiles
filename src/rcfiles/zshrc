# Vim configurations added by github.com/Jiggins/dotfiles

# ZSH Options
COMPLETION_WAITING_DOTS="true"
ENABLE_CORRECTION="true"
HYPHEN_INSENSITIVE="true"

export ZSH_CONFIG_DIR="${HOME}/.config/zsh"

setopt BANG_HIST                 # Treat the '!' character specially during expansion.
setopt EXTENDED_HISTORY          # Write the history file in the ":start:elapsed;command" format.
setopt INC_APPEND_HISTORY        # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY             # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire duplicate entries first when trimming history.
setopt HIST_IGNORE_DUPS          # Don't record an entry that was just recorded again.
setopt HIST_IGNORE_SPACE         # Don't record an entry starting with a space.
setopt HIST_SAVE_NO_DUPS         # Don't write duplicate entries in the history file.
setopt HIST_REDUCE_BLANKS        # Remove superfluous blanks before recording entry.
setopt HIST_VERIFY               # Don't execute immediately upon history expansion.

# Edit command line in $EDITOR
autoload -z edit-command-line
zle -N edit-command-line

# Prompt
# fpath=("${HOME}/git/dotfiles/src/config/zsh" $fpath)
fpath=("${ZSH_CONFIG_DIR}" $fpath)
autoload -U promptinit
promptinit
prompt custom

# ZSH style
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

# History options
HISTFILE="$HOME/.zsh_history"
HISTSIZE=10000
SAVEHIST=10000
HIST_STAMPS="yyyy-mm-dd"

# Vi mode
bindkey -v

bindkey '^a' beginning-of-line
bindkey '^e' end-of-line
bindkey '^P' up-history
bindkey '^N' down-history
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
bindkey '^r' history-incremental-search-backward
bindkey -M vicmd v edit-command-line

# Stop ^w from deleting `/`
autoload -U select-word-style && select-word-style bash

# 10ms for key sequences
export KEYTIMEOUT=1

# Disable zsh correction on shell arguments
# $ which vim
# "zsh: correct 'vim' to '.vim' [nyae]?"
unsetopt correct_all

autoload predict-on

# adds a directory to the end of PATH if the directory exists
function addpath() {
  if [ -d "$1" ]; then
    path+=("$1")
    return 0
  fi

  return 1
}

# Prevent duplicate entries in $PATH
typeset -U path

addpath "${HOME}/bin"
addpath "${HOME}/.local/bin"
addpath "${HOME}/.rvm/bin"
addpath "${HOME}/.cargo/bin"
addpath "${HOME}/.vim/bundle/powerline/scripts"
addpath "${HOME}/go/bin"
addpath "/usr/local/bin"
addpath "/usr/local/sbin"
addpath "/usr/local/opt/python/libexec/bin"
addpath "/usr/local/texlive/2016basic/bin/x86_64-darwin"

# Solidify PATH changes for the rest of the shell sessions
export PATH

# # zplug
export ZPLUG_HOME="${ZSH_CONFIG_DIR}/zplug"

if ! [[ -d "${ZPLUG_HOME}" ]]; then
  echo "Installing zplug"
  git clone https://github.com/zplug/zplug "${ZPLUG_HOME}"
fi

source "${ZPLUG_HOME}/init.zsh"

zplug 'zplug/zplug', hook-build:'zplug --self-manage'

zplug "RobSis/zsh-completion-generator"
zplug "trapd00r/LS_COLORS", hook-build:"dircolors -b LS_COLORS > c.zsh"
zplug "zsh-users/zsh-autosuggestions"
zplug "zsh-users/zsh-syntax-highlighting"
zplug "lib/completion", from:oh-my-zsh

zplug check || zplug install
zplug load

[ -f "${HOME}/.fzf.zsh" ]           && source "${HOME}/.fzf.zsh"
[ -f "${HOME}/.rvm/scripts/rvm" ]   && source "${HOME}/.rvm/scripts/rvm"
[ -f "/usr/local/rvm/scripts/rvm" ] && source /usr/local/rvm/scripts/rvm

export EDITOR=$(which vim)
export LESS="SR"
export PASSWORD_STORE_ENABLE_EXTENSIONS=true

if command -v 'bat' >/dev/null; then
  alias cat='bat'
  export BAT_STYLE="grid,header"
fi

alias cgrep='grep --color=always'
alias focusfix='printf "\e[?1004l"'
alias gls='git ls-files'
alias isodate='date +%Y-%m-%d'
alias ll='ls -lh'
alias path='echo $PATH | sed "s/:/\n/g"'
alias tidy='tidy -quiet -indent'
alias tree='tree -C'

# Colours and text formatting
export BOLD=$(tput bold)
export RED=$(tput setaf 1)
export GREEN=$(tput setaf 2)
export YELLOW=$(tput setaf 3)
export BLUE=$(tput setaf 3)
export RESET=$(tput sgr0)
export CLEARLN="\r$(tput el)"

# Common regex
export re_IP='\b([0-9]{1,3}\.){3}[0-9]{1,3}\b'

if command -v pigz > /dev/null; then
  alias tarz='tar --use-compress-program=pigz'
fi

# https://github.com/nvbn/thefuck
if command -v thefuck > /dev/null; then
  eval $(thefuck --alias)
fi

# FZF
if command -v fd > /dev/null; then
  export FZF_DEFAULT_COMMAND="fd --type file --color=always"
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
  export FZF_DEFAULT_OPTS="--ansi"

  if command -v bat > /dev/null; then
    export FZF_DEFAULT_OPTS="${FZF_DEFAULT_OPTS} --preview '[[ -f {} ]] && bat --style grid --color always {}' --preview-window down:10"
  fi
fi

source "${ZSH_CONFIG_DIR}/taskwarrior.zsh"
source "${ZSH_CONFIG_DIR}/tmux.zsh"

case "$(uname -s)" in
  Linux*)  source "${ZSH_CONFIG_DIR}/linux.zsh" ;;
  Darwin*) source "${ZSH_CONFIG_DIR}/macos.zsh" ;;
esac

# Local override
if [[ -f "${ZSH_CONFIG_DIR}/local.zsh" ]]; then
  source "${ZSH_CONFIG_DIR}/local.zsh"
fi

# Codi
# Usage: codi [filetype] [filename]
function codi() {
  local syntax="${1:-python}"
  shift
  vim -c \
    "let g:startify_disable_at_vimenter = 1 |\
    set bt=nofile ls=0 noru nonu nornu |\
    hi ColorColumn ctermbg=NONE |\
    hi VertSplit ctermbg=NONE |\
    hi NonText ctermfg=0 |\
    Codi $syntax" "$@"
}

function compress() {
  if [[ -z "$1" ]]; then
    echo "usage: compress [files]"
    return 1
  fi

  tar -cf - "$@" | pigz | pv
}

function intercalate() {
  ghc -e "getContents >>= putStrLn . foldr1 (\a c -> a ++ \"${1}\" ++ c) . words"
}

# This should be done by `git init` but it does not work sometimes
function install-git-hooks() {
  [[ -d .git ]] \
    && rsync -Lav --delete ~/.git_template/hooks/ .git/hooks
}

function js() {
  jshon "$@" | pygmentize -l 'json'
}

# use vim ex commands in a UNIX pipe
function vp() {
    vim - -u NONE -es '+1' "+$*" '+%print' '+:qa!' | tail -n +2
}

# use vim normal mode commands in a UNIX pipe
function vn() {
    vim - -u NONE -es '+1' "+normal $*" '+%print' '+:qa!' | tail -n +2
}

function map() {
  while read i; do
    eval "$* $i"
  done
}

function man() {
    LESS_TERMCAP_md=$'\e[01;31m' \
    LESS_TERMCAP_me=$'\e[0m' \
    LESS_TERMCAP_se=$'\e[0m' \
    LESS_TERMCAP_so=$'\e[01;44;33m' \
    LESS_TERMCAP_ue=$'\e[0m' \
    LESS_TERMCAP_us=$'\e[01;32m' \
    command man "$@"
}

new-md() {
    local file="$(isodate)-${*// /-}.md"
    local length="${#$(echo $*)}"
    printf "${*}\n"               >> $file
    printf "=%.0s" {1..${length}} >> $file
    printf "\n\n"                 >> $file
    ${EDITOR} $file
}

function grep-after() {
  perl -nle 'print $& if m{(?<=${1}\s)\S+}'
}

# Join array with delimiter
# From https://stackoverflow.com/a/17841619
function join_by {
  local IFS="$1"
  shift
  echo "$*"
}

function remove-spaces() {
  for i in $1; do                                                                                                                                                                                                                            Â 
    if echo $i | grep -E ' ' > /dev/null; then
      echo "$i" "${i// /-}"
    fi
  done
}

function pip() {
  if (( $# < 1 )); then
    echo "usage: $0 <package> [<packages>]"
    return 1
  fi

  for i in 3 2; do
    "python${i}" -m pip "${@}" \
      || echo "${YELLO}Warning: Failed to install ${*} for python${i}${RESET}"
  done

  return $?
}

function mmail() {
  pandoc --standalone --from markdown --to rtf --template bootstrap.rtf --output - $1 | pbcopy
}
